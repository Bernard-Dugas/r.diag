#     if defined (AUTO_DOC)
*
*     CALL SEQUENCE...
*      ttesta  XBAR VX YBAR VY MDIF TVALUE KVALUE DVALUE MASK \ 
*      INPUT OUTPUT [ -a VAL1 -lx VAL2 -ly VAL3 -kind VAL4 [VAL5] [VAL6] ]
*
*
*     DESCRIPTION...
*      ttesta  - CONDUCT AN "APPROXIMATE" DIFFERENCE OF MEANS TEST.
*
*
*     AUTHORS - F.Zwiers, JAN 25/85,
*     LAST REVISION: $Header: /home/cvs/RMNLIB/utils/rdiag/lspgm/ttesta.ptn,v 4.20 2014/09/25 20:20:55 dugas Exp $
*
*
*     PURPOSE - CONDUCT A DIFFERENCE OF MEANS TEST. THE TEST WHICH IS CONDUCTED
*               IS THE 'APPROXIMATE T-TEST' WHICH MAKES  NO ASSUMPTION  ABOUT THE
*               EQUALITY OF VARIANCES. HOWEVER, ALL THE OTHER ASSUMPTIONS USUALLY
*               MADE WHEN CONDUCTING A T-TEST (I.E., INDEPEDENT OBSERVATIONS FROM
*               NORMAL POPULATIONS IN EACH SAMPLE, INDEPENDENT SAMPLES) MUST STILL
*               BE SATISFIED. UPON RETURN, THE PROGRAM REPORTS THE COMPUTED T-TEST
*               VALUES  IN  TVALUE  AS WELL AS  RETURNING THE  K-VALUE AND D-VALUE
*               WHICH ARE RESPECTIVELY THE TRANSFORMED SIGNIFICANCE LEVELS AND THE
*               ACCEPTANCE/REJECTION FLAG.
*
*     NOTE - 1) TESTS ARE NOT PERFORMED WHEN THE "POOLED VARIANCE" IS
*               APPROXIMATELY EQUAL TO THE NOISE INDUCED BY THE PACKING
*               ALGORITHM.
*            2) INTERPRETATION OF VX AND VY FILES IS CONTROLED BY INPUT
*               PARAMETER IVAR.
*            3) MASK FILE NEED NOT BE SPECIFIED IN THE PROGRAM CALLING
*               SEQUENCE.(SEE OUTPUT FILES SECTION BELOW).
*
*
*     INPUT FILES...
*      XBAR   = FIELDS OF MEANS OF THE 'X-CONTROL' VARIABLE.
*     (IF (IVAR.EQ.0) THEN
*     (VX     = FIELDS OF THE VARIANCE OF X.
*     (         SUM(X(I)-XBAR)**2/NX, I=1,...,NX.
*     (OTHERWISE,
*     (VX     = FIELDS OF THE STANDARD DEVIATION OF X.
*     (         SQRT(SUM(X(I)-XBAR)**2/(NX-1)), I=1,...,NX.
*      YBAR   = SAME AS XBAR EXCEPT FOR THE 'Y-EXPERIMENT' VARIABLE.
*      VY     = SAME AS VX   EXCEPT FOR THE 'Y-EXPERIMENT' VARIABLE.
*
*     OUTPUT FILES...
*      MDIF   = FIELDS OF MEAN DIFFERENCES COMPUTED AS D=(YBAR-XBAR).
*      TVALUE = FIELDS OF 'T-VALUES' GIVEN BY
*
*                     T    = (YBAR-XBAR)/SDIF
*
*                     SDIF = SQRT( AXT+AYT )
*
*                     AXT  = VX/(NX-1)
*
*                     AYT  = VY/(NY-1)
*
*               WHERE, VX AND VY ARE THE VARIANCES OF X AND Y
*               VARIABLES RESPECTIVELY.
*      NOTE   - THE APPROPRIATE NUMBER OF DEGREES OF FREEDOM, (NDF),
*               ARE ESTIMATED FROM THE OBSERVATIONS AS FOLLOWS:
*               (SEE FOR EXAMPLE, PROB AND STATS FOR ENGINEERS
*               AND SCIENTISTS BY WALPOLE AND MYERS).
*
*                     NDF  = MAX( 1,INT( ANUM/ADEN+0.5 ) )
*
*                     ANUM = ( AXT+AYT )**2
*
*                     ADEN = AXT**2/(NX-1) + AYT**2/(NY-1)
*
*               WHERE, AXT AND AYT ARE AS DEFINED ABOVE.
*      KVALUE = FIELDS OF 'K-VALUES' OF TRANSFORMED SIGNIFICANCE LEVELS
*               COMPUTED AS K IN:
*
*                     P    = ALPHA/( 5**(K-1) )
*
*               OR    K    = LOG ( ALPHA/P )+1
*                               5
*
*               WHICH INDICATES THAT THE OBSERVED STATISTIC IS SIGNIFICANT
*               AT THE  "ALPHA/(5**(K-1))" SIGNIFICANCE LEVEL.
*               THUS K=1 INDICATES THAT THE OBSERVED STATISTIC IS JUST
*               SIGNIFICANT AT THE ALPHA SIGNIFICANCE LEVEL.
*               IF PLOTTED WITH UNIT CONTOUR INTERVALS, SUCCESSIVE
*               CONTOURS WILL ENCLOSE REGIONS WHERE LOCALLY IT IS
*               FIVE TIMES AS UNLIKELY THAT VALUES OF THE OBSERVED
*               STATISTICS ARE CONSISTENT WITH THE NULL HYPOTHESIS
*               THAN IN REGIONS OUTSIDE THE NEXT LOWER CONTOUR.
*      DVALUE = FIELDS OF 'D-VALUES' COMPUTED AS:
*                       __
*                       ! 0  IF THE NULL HYPOTHESIS IS ACCEPTED
*                   D = !
*                       ! 1  IF THE NULL HYPOTHESIS IS REJECTED
*                       --
*
*      MASK   = FIELDS OF 'MASK-VALUES' COMPUTED AS:
*                       __
*                       ! 1  IF THE TEST WAS     CONDUCTED
*                MASK = !
*                       ! 0  IF THE TEST WAS NOT CONDUCTED
*                       --
*
*      NOTE   - THE MASK FILE IS NOT RETURNED IF THE PROGRAM IS NOT
*               CALLED WITH OUTPUT FILE MASK.
* 
*
*     INPUT PARAMETERS...
*
*      ALPHA (a)     = THE SIGNIFICANCE LEVEL OF THE TEST.
*      ANX   (lx)    = THE NUMBER OF OBSERVATIONS IN THE CONTROL    DATA SET,
*      ANY   (ly)    = THE NUMBER OF OBSERVATIONS IN THE EXPERIMENT DATA SET.
*                      THE ACTUAL VALUE USED ARE RATHER NX AND NY DEFINED AS
*                      NX = INT( ANX ) AND  NY = INT( ANY ).
*                      IF NX >= 1 , NY >= 1 THEN
*                         NX AND NY FROM THE INPUT CARD ARE USED IN THE
*                         COMPUTATION OF THE NUMBER OF DEGREES OF FREEDOM.
*                      OTHERWISE,
*                         THE NX AND NY VALUES NEEDED IN THE COMPUTATION ARE
*                         OBTAINED FROM THE RECORD LABELS OR FROM THE IP3
*                         DESCRIPTORS, DEPENDING ON THE FILE TYPE OF THE
*                         XBAR AND YBAR FILES.
*      KIND (kind/1) = -1, REJECT THE NULL HYPOTHESIS IF P > 1 - ALPHA
*                         (IE - CONDUCT A ONE TAILED TEST AND REJECT IF
*                               THE TEST STATISTIC IS UNUSUALLY SMALL).
*                    =  0, INDICATES A TWO TAILED TEST (REJECT IF
*                          P > 1 - ALPHA/2   OR IF  P < ALPHA/2)
*                          (IE - REJECT IF THE TEST STATISTIC IS
*                                UNUSUALLY SMALL OR LARGE).
*                    =  1, REJECT THE NULL HYPOTHESIS IF P < ALPHA
*                          (IE - CONDUCT A ONE TAILED TEST AND REJECT IF
*                                THE TEST STATISTIC IS UNUSUALLY LARGE).
*      IVAR (kind/2) = A FLAG USED TO DETERMINE THE CONTENTS
*                      OF THE VX AND VY FILES. THAT IS :
*                      IF (IVAR.EQ.0) THEN
*                        THE VX AND VY FILES CONTAIN THE
*                        VARIANCES OF X AND Y, RESPECTIVELY.
*                      OTHERWISE,
*                        VX AND VY FILES CONTAIN THE UNBIASED ESTIMATES OF
*                        THE STANDARD DEVIATIONS OF X AND Y, RESPECTIVELY.
*      MATCH(kind/3) = A FLAG USED TO DETERMINE IF THE VARIABLE NAMES IN
*                      XBAR AND YBAR *MUST* BE MATCHED. In this case, An
*                      unmatched variables will generate an error abort.
*                      THE DEFAULT BEHAVIOUR IS NOT TO CHECK THIS (=NO).
*
*      READ(5,5010) ALPHA,ANX,ANY,KIND,IVAR
*5010  FORMAT(10X,E10.0,2F5.0,2I5)
*
*
*     EXAMPLE OF INPUT CARD...
*
**TTESTA.      5.E-2  10.   5.    0    1
*
*        0123456789012345678901234567890123456789012345678901234567890
*        1         2         3         4         5         6         7
*
*
*     EXIT CONDITIONS...
*       0 NO PROBLEMS
*
*     FATAL CONDITIONS...
*       1 PREMATURE EOF ON UNIT 5
*       2 PREMATURE EOF ON UNIT 1 AND/OR 3
*       3 INITIAL ILLEGAL DATA TYPE OR SDX/SDY NOT COMPATIBLE
*       4 CANNOT READ FIRST RECORD IN XBAR
*       5 PARAMETER NX TOO SMALL
*       6 CANNOT READ RECORD IN VX
*       7 MORE THAN ONE SIZE IN XBAR AND/OR VX
*       8 CANNOT READ FIRST RECORD IN YBAR
*       9 PARAMETER NY TOO SMALL
*      10 CANNOT READ RECORD IN VY
*      11 MORE THAN ONE SIZE IN YBAR AND/OR VY
*      12 MORE THAN ONE SIZE IN XBAR AND/OR YBAR
*      13 MEMORY ALLOCATION PROBLEM
*
*     NON-FATAL CONDITIONS...
*     101 VERY SMALL VALUES FOUND IN VX OR VY
*     102 NAMES OF MEAN FIELDS DO NOT MATCH
*
#     endif
#     if defined (RDIAG_LICENCE)
!---------------------------------- LICENCE BEGIN -------------------------------
! R.DIAG - Diagnostic tool kit for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This code is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------
#     endif
#     if !defined (NO_SOURCE)
      PROGRAM ttesta
*     PROGRAM TTESTA (XBAR,        VX,            YBAR,          VY,
*    1                MDIF,        TVALUE,        KVALUE,        DVALUE,
*    2                             MASK,          INPUT,         OUTPUT,)
*    3          TAPE1=XBAR,  TAPE2=VX,      TAPE3=YBAR,    TAPE4=VY,
*    4         TAPE10=MDIF, TAPE11=TVALUE, TAPE12=KVALUE, TAPE13=DVALUE,
*    5                      TAPE14=MASK,    TAPE5=INPUT,   TAPE6=OUTPUT)
*     ------------------------------------------------------------------
*     $Log: ttesta.ptn,v $
*     Revision 4.20  2014/09/25 20:20:55  dugas
*     Inserer le texte de la licence LPGPL 2.1 pour R.DIAG.
*
*     Revision 4.19  2013/03/21 21:30:50  bernard
*     Corriger l'allocation dynamique de DIFF, FMASK et IBUF.
*
*     Revision 4.18  2011/11/22 19:46:54  dugas
*     Ajouter le mode 'FORCE NAME MATCH'.
*
*     Revision 4.17  2008/07/24 19:42:59  dugas
*     Utiliser CALCEPS8 plutot que CALCEPS.
*
*     Revision 4.16  2008/05/03 21:06:30  dugas
*     Modification generale pour r.gppf.
*
*     Revision 4.15  2008/04/25 21:10:56  dugas
*     Toujours utiliser GET_IP1_STRING avant d'imprimer un niveau vertical.
*
*     Revision 4.14  2007/12/20 21:17:51  dugas
*     Faire appel aux routines PRTLAB/PRTLAB2.
*
*     Revision 4.13  2005/04/14 18:15:49  dugas
*     Assurer une allocation minimale a la lecture de MAXW mots.
*
*     Revision 4.12  2003/07/05 14:52:24  dugas
*     Implanter SETRABT pour le traitement de RECABT.
*
*     Revision 4.11  2002/04/22 15:30:47  dugas
*     Corriger les declarations de NAMEX, NAMEY et TYPE.
*
*     Revision 4.10  2001/10/23 15:05:40  armnrbd
*     Implanter RECABT='&NON' et sauver les noms de variables
*     d'entrees dans l'etiquette des variables de sorties.
*
*     Revision 4.9  2001/03/17 21:41:37  armnrbd
*     Remplacer GETPARI par GETHIGH.
*
*     Revision 4.8  2000/03/20 21:41:11  armnrbd
*     Associer un seul champs par pointeur.
*
*     Revision 4.7  1998/10/19 19:51:05  armnrbd
*     Modifier la documentation.
*     Implanter le support de RPBLOC.
*
*     Revision 4.6  1998/07/03  02:26:44  armnrbd
*     Allonger le format d'ecriture de la date sur le listing.
*
*     Revision 4.5  1997/09/26  18:15:42  armnrbd
*     Deuxieme phase de la generalisation de taille_entete.
*
*     Revision 4.4  1997/09/26  17:48:03  armnrbd
*     Remplacer buf(16) par buf(taille_entete).
*
*     Revision 4.3  1997/01/30  03:19:39  armnrbd
*     Corriger la definition du MASK.
*
*     Revision 4.2  1996/12/12  19:58:42  armnrbd
*     Corriger la sorties de K et D.
*     Ajouter le support de fichiers SQI.
*
*     Revision 4.1  1996/11/12  19:41:23  armnrbd
*     Ajouter le support de grilles de type SUBA.
*
*     Revision 4.0  1995/12/21  20:26:21  armnrbd
*     Version initiale.
*

*     JUN 05/94 - B.DENIS  (MODIFY CALCULATION OF XSCALE/YSCALE)
*     MAR 07/94 - F.MAJAESS (MODIFY DATA SECTION)
*     AUG 04/92 - E. CHAN   (MODIFY EXTRACTION/CALCULATION OF XMAX, XMIN,       
*                            AND XSCALE DUE TO IMPLEMENTATION OF NEW PACKER)   
*     JAN 29/92 - E. CHAN   (CONVERT HOLLERITH LITERALS TO ASCII)               
*     OCT 10/89 - F.MAJAESS (FIX ERROR IN CALCULATING PACKING NOISE VALUE)      
*     APR 29/88 - F.MAJAESS (REPLACE ABORT EXIT BY WARNING EXIT FOR 
*                            UNMATCHED MEAN FIELD NAMES)
*     NOV 06/87 - F.MAJAESS (ALLOW SUPPLYING VARIANCE OR UNBIASED 
*                            ESTIMATES OF THE STANDARD DEVIATION AND
*                            IMPLEMENT COMPUTATIONS OF K AND D-VALUES). 
*     JAN 25/85 - F. ZWIERS 

*---------------------------------------------------------------------------- 
      IMPLICIT     none

      INTEGER,     PARAMETER :: HEAD = taille_entete
      INTEGER,     PARAMETER :: MAXLEV = 999

      REAL         LO,HI
      CHARACTER    LIGNE*80,SLEV*15,CMATCH*15
      LOGICAL      OK,OK1,OK2,OK3,OK4,LNXNY,VAR,
     +             NRMLXIT,MASK,MATCH,NORETRY,NMATCH  
      CHARACTER(4) NAMEX,NAMEY,NAMEVX,NAMEVY,PKTYP,
     +             TYPE,CD,CK,CT,GRID,SUBA,ZONL,CMDIF,CMASK
      REAL(8)      ALPHA,SDIF,WK1,WK2,WK3,WK4,ANX,ANUM,ADEN,FNX,FNY,
     +             ANY,EPSX(MAXLEV),EPSY(MAXLEV),EPS(MAXLEV),FACT
      INTEGER      IBUFW(HEAD),IBUFX(HEAD),IBUFY(HEAD),IBUFZ(HEAD),
     +             LEV(MAXLEV),NLEV,NLEVX,NLEVY,NLVVX,NLVVY,IPM,RK,
     +             IO,NX,NY,MAXW,NWDS,WHERE,NF,LEN,LEN1,LEN2,LEN3,
     +             LEN4,MAXW1,MAXW2,MAXW3,MAXW4,XPAK,YPAK,IER,FNXM1,
     +             FNYM1,NREC,KIND,IVAR,CNT,I,L,NDFP,MEM

      INTEGER,    DIMENSION(:),ALLOCATABLE        :: IBUF
      REAL(8),    DIMENSION(:),ALLOCATABLE,TARGET :: D,K,AXT,AYT
      REAL(8),    DIMENSION(:),ALLOCATABLE        :: T,P,DIF,FMASK
      REAL(8),    DIMENSION(:),POINTER            :: VY,VX,XBAR,YBAR

#     include     "zlabel.cdk"

      CHARACTER(4),EXTERNAL :: GETYP
      REAL(8),     EXTERNAL :: CALCEPS8
      LOGICAL,     EXTERNAL :: RPBLOC,SETIO64,is_on
      INTEGER,     EXTERNAL :: GETSIZ2,GETHIGH,GETSAMPLZ
      EXTERNAL     JCLPNT,GETSTAT,GETLIGN,BURNF,PROBT,HTEST,
     +             WRITLEV2,GETSET2,CMPLBL,XIT,PUTSET2,PUTHIC

      DATA         GRID  /'GRID'/,
     +             SUBA  /'SUBA'/,
     +             ZONL  /'ZONL'/,
     +             CMDIF /'MDIF'/,
     +             CMASK /'MASK'/
      DATA         CD    /'D'/,
     +             CK    /'K'/,
     +             CT    /'T'/

      DATA         NRMLXIT,MATCH,MASK,NMATCH / 2*.TRUE.,2*.FALSE. /

      CHARACTER    NOMPRG*256
      COMMON      /PROGNAM/ NOMPRG
*===================================================================

      NOMPRG =
     +'$Source: /home/cvs/RMNLIB/utils/rdiag/lspgm/ttesta.ptn,v $'

*---------------------------------------------------------------------- 
      CALL SETRABT( '&NON' )

***    ACCEDER LES FICHIERS D'E/S.

      NF = 11 
      CALL JCLPNT( NF, 1,2,3,4, 10,11,12,13, 14, 5,6 )

      NREC = 0

***    RETREIVE FILE TYPE.

      PKTYP = GETYP( 1 )

      CALL GETSTAT( 14,LIGNE )
      IF (LIGNE.EQ.'OLD' .OR.
     +    LIGNE.EQ.'NEW') MASK = .TRUE.

***    SETUP FOR 64-BIT I/O.

      OK = SETIO64(.TRUE.)

      CMATCH= ' '
      NAMEVX = ' ' ; NAMEX = ' '
      NAMEVY = ' ' ; NAMEY = ' '

      ZNAME  = ' '
      ZTYPE  = ' '
      ZLABEL = ' '

      IF (RPBLOC( ' ',LIGNE ))                                 THEN

***        CHECK PARAMETRE BLOCK.

          ALPHA = 0.0
          ANX   = 0.0
          ANY   = 0.0
          KIND  =  0
          IVAR  =  0

          OK = RPBLOC('KIND',LIGNE)
          IF (OK) READ(LIGNE,0005, END=8000,ERR=8000,IOSTAT=IO ) KIND

          OK = RPBLOC('KIND2',LIGNE)
          IF (OK) READ(LIGNE,0005, END=8000,ERR=8000,IOSTAT=IO ) IVAR

          OK = RPBLOC('KIND3',LIGNE)
          IF (OK) CMATCH = LIGNE

          OK = RPBLOC('A',LIGNE)
          IF (OK) READ(LIGNE,0010, END=8000,ERR=8000,IOSTAT=IO ) ALPHA

          OK = RPBLOC('LX',LIGNE)
          IF (OK) READ(LIGNE,0010, END=8000,ERR=8000,IOSTAT=IO ) ANX

          OK = RPBLOC('LY',LIGNE)
          IF (OK) READ(LIGNE,0010, END=8000,ERR=8000,IOSTAT=IO ) ANY

      ELSE

***        GET ALPHA, ANX, ANY, KIND AND IVAR.

          CALL GETLIGN( 5, LIGNE,80, OK)

          IF (.NOT.OK)                                         THEN
              GOTO 8000
          ELSE
              READ( LIGNE,5010, END=8000,ERR=8000,IOSTAT=IO )
     +                          ALPHA,ANX,ANY,KIND,IVAR
              CALL BURNF
          END IF

      END IF

      OK = .false. ; IF (CMATCH /= ' ') OK = is_on( CMATCH )

      IF (OK)                                                  THEN
          if (pktyp(1:2) == 'SQ')                              THEN
              NMATCH = OK
              WRITE(6,6160)
          ELSE
              WRITE(6,6161) pktyp
          END IF
      END IF

      WRITE(6,6110) ALPHA,ANX,ANY,KIND,IVAR 
      NX = INT( ANX+0.0001 )
      NY = INT( ANY+0.0001 )

***    CHECK NX, NY AND IVAR.

      IF ((NX.LT.1) .OR. (NY.LT.1))                            THEN
          LNXNY =.TRUE.
      ELSE
          LNXNY =.FALSE. 
      END IF 

      IF (IVAR.EQ.0)                                           THEN 
          VAR =.TRUE.
      ELSE
          VAR =.FALSE. 
      END IF 

***    DETERMINE SIZES AND ALLOCATE MEMORY.

  100 MAXW1 = GETSIZ2( 1, IBUFW,LEN1,NWDS,XPAK, LEV,NLEV )
      MAXW2 = GETSIZ2( 2, IBUFX,LEN2,NWDS,YPAK, LEV,NLVVX)
      MAXW3 = GETSIZ2( 3, IBUFY,LEN3,NWDS,XPAK, LEV,NLEVY)
      MAXW4 = GETSIZ2( 4, IBUFZ,LEN4,NWDS,YPAK, LEV,NLVVY)

      IF (MAXW1.LE.0 .OR.
     +    MAXW2.LE.0 .OR.
     +    MAXW3.LE.0 .OR.
     +    MAXW4.LE.0) GOTO 9000

      LEN  = MAX( LEN1 ,LEN2 ,LEN3 ,LEN4  )
      MAXW = MAX( MAXW1,MAXW2,MAXW3,MAXW4 )

      WRITE( TYPE,0004) IBUFW(1)
      IF (TYPE.EQ.'ZONL') NWDS = IBUFW(5)

      CALL WRITLEV2( LEV,NLEV,IBUFW )

      CALL CMPLBL( 0,IBUFW, 0,IBUFY, OK )

      CALL PRTLAB( IBUFW ) ; CALL PRTLAB( IBUFY )

      IF (NREC .EQ. 0    .AND.
     +   (.NOT.OK        .OR.
     +    NLEV .NE.NLEVY .OR.
     +   (TYPE .NE.GRID .AND.
     +    TYPE .NE.SUBA .AND.
     +    TYPE .NE.ZONL ) ) )                                  THEN
          WRITE(6,6003) NLEV,NLEVY,NLVVX,NLVVY
          CALL                                     XIT(' Ttesta ',-3 )
      END IF

      MEM = MAX( MAXW,NWDS*NLEV )

      ALLOCATE( D(MEM),T(MEM),K(MEM),P(MEM),AXT(MEM),AYT(MEM),
     +          DIF(MEM),FMASK(MEM),IBUF(LEN), STAT=IER )

      IF (IER /= 0)                                            THEN
          WRITE(6,6013) IER,8*MEM,LEN
          CALL                                     XIT(' Tteste ',-13)
      END IF

      ! Equivalences.
      VY => D
      VX => K
      XBAR => AXT
      YBAR => AYT
*-----------------------------------------------------------------------

 1000 CONTINUE

***        READ XBAR.

          CALL GETSET2( 1, XBAR, LEV,NLEVX, IBUF,MAXW,OK ) 

          IF (.NOT.OK)                                         THEN 

              IF (NREC.EQ.0)                                   THEN
                  WRITE(6,6004) 
                  CALL                             XIT(' Ttesta ',-4 ) 
              END IF

              IF (MAXW.EQ.0)                                   THEN
                  DEALLOCATE( D,T,K,P,AXT,AYT,DIF,FMASK,IBUF )
                  GOTO 100
              END IF

              WRITE(6,6100) NREC 

              IF (NRMLXIT.AND.MATCH)                           THEN
                  CALL                             XIT(' Ttesta ', 0 )
              ELSE 
                  IF (.NOT.NRMLXIT) CALL           XIT(' Ttesta ',-101)
                  IF (.NOT.MATCH)   CALL           XIT(' Ttesta ',-102)
              END IF

          END IF

***        CHECK SIZE CONSISTENCY.

          CALL CMPLBL( 0,IBUF, 0,IBUFW, OK1 )

          DO  I=1,HEAD
              IBUFW(I) = IBUF(I) 
          END DO

          WRITE(NAMEX,0004) IBUFW(3)

          IF (LNXNY)                                           THEN
              IF (PKTYP(1:2).EQ.'SQ')                          THEN
                  NX = GETSAMPLZ( RK,HI,LO,IPM,IBUF )
              ELSE
                  NX = IBUFW(2)
              END IF
          END IF
          IF (NX.LE.1)                                         THEN 
              WRITE(6,6005) NX
              CALL                                 XIT(' Ttesta ',-5 ) 
          END IF

          FNX   = FLOAT( NX ) 
          FNXM1 = FNX-1.0 

***        READ VX.

          CALL GETSET2( 2, VX, LEV,NLVVX, IBUF,MAXW,OK )

          IF (.NOT.OK)                                         THEN
              IF (MAXW.EQ.0)                                   THEN
                  CALL PRECEDE( 1,NLEVX )
                  DEALLOCATE( D,T,K,P,AXT,AYT,DIF,FMASK,IBUF )
                  GOTO 100
              END IF
              WRITE(6,6006) 
              CALL                                 XIT(' Ttesta ',-6 ) 
          END IF

          WRITE(NAMEVX,0004) IBUF(3)

***        CHECK SIZE CONSISTENCY.

          CALL CMPLBL( 0,IBUF, 0,IBUFW, OK2 )

          WRITE(TYPE,0004) IBUFW(1)

          OK = (OK1 .AND. OK2)

          IF (.NOT.OK        .OR.
     +        NLEVX.GT.NLEV  .OR.
     +        NLEVX.NE.NLVVX .OR.
     +       (TYPE .NE.GRID .AND.
     +        TYPE .NE.SUBA .AND.
     +        TYPE .NE.ZONL )   )                              THEN
              WRITE(6,6100) NREC
              WRITE(6,6003) NLEVX,NLVVX
              CALL PRTLAB( IBUFW ) ; CALL PRTLAB( IBUF )
              CALL                                 XIT(' Ttesta ',-7 )
          END IF

          IF (.NOT.VAR)                                        THEN

***        CONVERT UNBIASED STANDARD DEVIATION
***        ESTIMATES TO VARIANCES.

              FACT = (FNX-1.0)/FNX
              DO  I=1,NWDS*NLEVX
                  VX(I) = FACT*( VX(I)**2 )
              END DO

          END IF

          DO  L=1,NLEVX
              WHERE = (L-1)*NWDS+1
              EPSX(L) = CALCEPS8( VX(WHERE),NWDS,IBUF(8) )
          END DO

          IF (NREC.EQ.0) CALL PRTLAB( IBUF )

***        READ YBAR.

          NORETRY = .TRUE.

 1100     CALL GETSET2( 3, YBAR, LEV,NLEVY, IBUF,MAXW,OK ) 

          IF (.NOT.OK)                                         THEN 
              IF (MAXW.EQ.0)                                   THEN
                  IF (ZNAME == ' ')                            THEN
                                  CALL PRECEDE( 1,NLEVX )
                     IF (NX.GT.1) CALL PRECEDE( 2,NLVVX )
                  ELSE
                                  CALL PRECEDE( 1,-1 )
                     IF (NX.GT.1) CALL PRECEDE( 2,-1 )
                  END IF
                  DEALLOCATE( D,T,K,P,AXT,AYT,DIF,FMASK,IBUF )
                  GOTO 100
              END IF
              IF (NORETRY)                                     THEN
                  CALL PRECEDE( 3,-1 )
                  ZNAME = NAMEX ; NORETRY = .FALSE.
                  GOTO 1100
              ELSE
                  WRITE(6,6008)
                  CALL                             XIT(' Ttesta ',-8 ) 
              END IF
          END IF

          WRITE(NAMEY,0004) IBUF(3)

***        CHECK FOR POSSIBLE MISSMATCH IN INPUT VARIABLES.

          IF (NMATCH .AND. NAMEX /= NAMEY)                     THEN
              CALL PRECEDE( 3,-1 )
              ZNAME = NAMEX
              WRITE(6,6151) trim(NAMEX),trim(NAMEY)
              GOTO 1100
          END IF

          ZNAME = ' '

***        CHECK SIZE CONSISTENCY.

          CALL CMPLBL( 0,IBUF, 0,IBUFY, OK1 )

          DO  I=1,HEAD 
              IBUFY(I) = IBUF(I) 
          END DO

          IF (LNXNY)                                           THEN
              IF (PKTYP(1:2).EQ.'SQ')                          THEN
                  NY = GETSAMPLZ( RK,HI,LO,IPM,IBUF )
              ELSE
                  NY = IBUFY(2)
              END IF
          END IF 
          IF (NY.LE.1)                                         THEN 
              WRITE(6,6009) NY
              CALL                                 XIT(' Ttesta ',-9 ) 
          END IF

          FNY   = FLOAT( NY ) 
          FNYM1 = FNY-1.0 

***        READ VY.

          NORETRY = .TRUE.

 1200     CALL GETSET2( 4, VY, LEV,NLVVY, IBUF,MAXW,OK )

          IF (.NOT.OK)                                         THEN
              IF (MAXW.EQ.0)                                   THEN
                  CALL PRECEDE( 1,NLEVX )
                  CALL PRECEDE( 2,NLVVX )
                  CALL PRECEDE( 3,NLEVY )
                  DEALLOCATE( D,T,K,P,AXT,AYT,DIF,FMASK,IBUF )
                  ZNAME = ' '
                  GOTO 100
              END IF
              IF (NORETRY)                                 THEN
                  CALL PRECEDE( 4,-1 )
                  ZNAME = NAMEVX ; NORETRY = .FALSE.
                  GOTO 1200
              ELSE
                  WRITE(6,6010) 
                  CALL                             XIT(' Ttesta ',-10 ) 
              END IF
          END IF

          WRITE(NAMEVY,0004) IBUF(3)

***        AGAIN, CHECK FOR POSSIBLE MISSMATCH IN INPUT VARIABLES.

          IF (NMATCH .AND. NAMEVX /= NAMEVY)                   THEN
              CALL PRECEDE( 4,-1 )
              ZNAME = NAMEVX
              WRITE(6,6152) trim(NAMEVX),trim(NAMEVY)
              GOTO 1200
          END IF

          ZNAME = ' '

***        CHECK SIZE CONSISTENCY.

          CALL CMPLBL( 0,IBUF, 0,IBUFY, OK2 )

          WRITE(TYPE,0004) IBUFY(1)

          OK = (OK1 .AND. OK2)

          IF (.NOT.OK        .OR.
     +        NLEVY.GT.NLEV  .OR.
     +        NLEVY.NE.NLVVY .OR.
     +       (TYPE .NE.GRID .AND.
     +        TYPE .NE.SUBA .AND.
     +        TYPE .NE.ZONL )   )                              THEN
              WRITE(6,6100) NREC
              WRITE(6,6003) NLEVY,NLVVY
              CALL PRTLAB( IBUFY ) ; CALL PRTLAB( IBUF )
              CALL                                 XIT(' Ttesta ',-11 )
          END IF

          IF (.NOT.VAR)                                        THEN

***        CONVERT UNBIASED STANDARD DEVIATION
***        ESTIMATES TO VARIANCES.

              FACT = (FNY-1.0)/FNY
              DO  I=1,NWDS*NLEVY
                  VY(I) = FACT*(VY(I)**2) 
              END DO

          END IF

          DO  L=1,NLEVY
              WHERE = (L-1)*NWDS+1
              EPSY(L) = CALCEPS8( VY(WHERE),NWDS,IBUF(8) )
          END DO

          IF (NREC.EQ.0)CALL PRTLAB( IBUF )

***        LAST CONSISTENCY CHECK.

          CALL CMPLBL( 0,IBUFW, 0,IBUFY, OK )

          IF (.NOT.OK  .OR.
     +        NLEVX.NE.NLEVY)                                  THEN
              WRITE(6,6100) NREC
              WRITE(6,6003) NLEVX,NLEVY
              CALL PRTLAB( IBUFW ) ; CALL PRTLAB( IBUFY )
              CALL                                 XIT(' Ttesta ',-12 ) 
          END IF

          IF (IBUFW(3).NE.IBUFY(3))                            THEN 
              IF (MATCH)                                       THEN
                  MATCH =.FALSE.
                  WRITE(6,6130)
              END IF
          ELSE
              NAMEY = ' '
          END IF

          DO  L=1,NLEVX
              EPS(L) = MIN( EPSX(L),EPSY(L) )
          END DO

***        COMPUTE THE MEAN DIFFERENCES. 

          DO  I=1,NWDS*NLEVX
              DIF(I) = YBAR(I)-XBAR(I) 
          END DO

          DO  I=1,NWDS*NLEVX
              AXT(I) = VX(I)/FNXM1 
              AYT(I) = VY(I)/FNYM1 
          END DO
 
***        COMPUTE THE T-STATISTICS AND CHECK
***        # OF OCCURENCES OF VERY SMALL SDIF. 

          DO  L=1,NLEVX

              CNT = 0

              DO  I=(L-1)*NWDS+1,L*NWDS

                  SDIF = SQRT( AXT(I)+AYT(I) )
                  IF (SNGL( SDIF ).LE.EPS(L))                  THEN 
                      FMASK(I) = 0.0 
                      T(I) = 0.0 
                      CNT  = CNT+1
                  ELSE 
                      FMASK(I) = 1.0 
                      T(I)     = DIF(I)/SDIF
                  END IF

              END DO

              IF (CNT.GE.1)                                    THEN
                  NRMLXIT =.FALSE. 
                  CALL GET_IP1_STRING( LEV(L),SLEV )
                  WRITE(6,6120) CNT,NAMEX,NWDS,SLEV
              END IF

          END DO

***        WRITE OUT THE RESULTS 

          DO  I=1,HEAD 
              IBUF(I)=IBUFW(I) 
          END DO

          IF (PKTYP(1:2).EQ.'SQ')                               THEN
              CALL PUTHIC( NAMEX,'ETIK1',IBUF )
              CALL PUTHIC( NAMEY,'ETIK2',IBUF )
              IF (PKTYP.NE.'SQ89')                              THEN
                  CALL PUTHIC( '  ','ETIK3',IBUF )
                  CALL PUTHIC( '  ','ETIK4',IBUF )
              END IF
          END IF

***        WRITE OUT MEAN DIFFERENCES. 

          READ(CMDIF,0004) IBUF(3)
          CALL PUTSET2( 10, DIF, LEV,NLEVX, IBUF,MAXW ) 
          IF (NREC.EQ.0) CALL PRTLAB( IBUF )

***        WRITE OUT T-VALUES. 

          DO  I=1,NWDS*NLEVX
              DIF(I) = T(I)
          END DO

          READ(CT,0004) IBUF(3)
          CALL PUTSET2( 11, DIF, LEV,NLEVX, IBUF,MAXW ) 
          IF (NREC.EQ.0) CALL PRTLAB( IBUF )

***        COMPUTE THE P-VALUES AS 
***    
***            P = PROB( STUDENT'S T > OBSERVED T )
***    
***            IE, P IS THE PROB THAT T IS GREATER THAN THE OBSERVED
***            VALUE ASSUMING THAT THE NULL HYPOTHESIS OF EQUALITY OF
***            MEANS IS CORRECT.
***    

          DO  I=1,NWDS*NLEVX

              IF (FMASK(I).EQ.0.0)                             THEN
                  NDFP = 1
              ELSE 
                  ANUM = ( AXT(I)+AYT(I) )**2
                  ADEN = AXT(I)**2/FNXM1 + AYT(I)**2/FNYM1 
                  NDFP = MAX( 1,INT( ANUM/ADEN+0.5 ) )
              END IF

              CALL PROBT( T(I),NDFP,1,P(I),WK1,WK2,WK3,WK4 )

          END DO

***        COMPUTE K AND D-VALUES. 

          CALL HTEST( P,NWDS*NLEVX, ALPHA, KIND, K,D )

***        WRITE OUT K-VALUES. 

          DO  I=1,NWDS*NLEVX
              VX(I) = K(I)
          END DO

          READ(CK,0004) IBUF(3)
          CALL PUTSET2( 12, VX, LEV,NLEVX, IBUF,MAXW )
          IF (NREC.EQ.0) CALL PRTLAB( IBUF )

***        WRITE OUT D-VALUES. 

          DO  I=1,NWDS*NLEVX
              VY(I) = D(I)
          END DO

          READ(CD,0004) IBUF(3)
          CALL PUTSET2( 13, VY, LEV,NLEVX, IBUF,MAXW )
          IF (NREC.EQ.0) CALL PRTLAB( IBUF )

          IF (MASK)                                            THEN

***            WRITE OUT MASK-VALUES.

              READ(CMASK,0004) IBUF(3)
              CALL PUTSET2( 14, FMASK, LEV,NLEVX, IBUF,MAXW ) 
              IF (NREC.EQ.0) CALL PRTLAB( IBUF )

          END IF

      ZNAME = ' '
      NREC = NREC+NLEVX
      GOTO 1000 

***    EOF ON UNIT #5. 

 8000 CONTINUE
      WRITE(6,6001) 
      CALL                                         XIT(' Ttesta ',-1 )

***    EOF ON UNIT 1 and/OR 3.

 9000 CONTINUE
      WRITE(6,6002) 
      CALL                                         XIT(' Ttesta ',-2 )

*-------------------------------------------------------------------------
 0004 FORMAT(A4)
 0005 format(BN,I5)
 0010 format(BN,E10.0)

 5010 FORMAT(10X,E10.0,2F5.0,2I5)

 6001 FORMAT('  The TTESTA card is missing.')
 6002 FORMAT(' ** Unable to begin reading mean files **.')
 6003 FORMAT(' Corresponding field labels do not match.'/
     +       ' OR'/
     +       ' Different number of levels found: ',2I5,:,2I5)
 6004 FORMAT(' File of means for variable X empty.')
 6005 FORMAT(' Illegal # of control obs. NX= ',I10)
 6006 FORMAT(' Unexpected eof on file of X variance.')
 6008 FORMAT(' Unexpected eof on file of Y means.')
 6009 FORMAT(' Illegal # of experimental obs. NY= ',I10)
 6010 FORMAT(' Unexpected eof on file of Y variance.')
 6013 FORMAT(' Error ',I10,' while allocating ',I10,' REAL(8)',
     +       ' words and ',I6,' INTEGER(4) words.')

 6100 FORMAT(' Conducted T-Tests on ',I5,' pairs of mean fields.')
 6110 FORMAT(' TTESTA.   ',E10.1,2F5.0,2I5)
 6120 FORMAT(' WARNING ',I6,' detected very small sdif out of ',A,I7,
     +       ' computed values at level ',A)
 6130 FORMAT(' ** NOTE - Unmatched mean field names **.')
 6151 FORMAT(' While looking for  ',A,', found ',A,' in means...')
 6152 FORMAT(' While looking for  ',A,', found ',A,' in varaiances...')
 6160 FORMAT(/' Strict name matching is ON !'/)
 6161 FORMAT(/' Strict name matching UNAVAILABLE !'/)
*-----------------------------------------------------------------------

      END 
#     endif
