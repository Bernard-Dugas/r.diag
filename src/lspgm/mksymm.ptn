#     if defined (AUTO_DOC)
*
*     SEQUENCE D'APPEL...
*      mksymm  IN OUT OUTPUT
*
*
*     AUTEUR - B.Dugas, NOV 30/88.
*     LAST REVISION: $Header: /home/cvs/RMNLIB/utils/rdiag/lspgm/mksymm.ptn,v 4.5 2014/09/25 20:20:51 dugas Exp $
*
*
*     DESCRIPTION...
*       CONSTRUIT UNE GRILLE SYMMETRIQUE A PARTIR D'UNE GRILLE 
*       HEMISPHERIQUE. LA CONSTRUCTION EST TELLE QU'UNE PROJECTION
*       SPECTRALE DU CHAMPS GLOBAL GARDERA UN MAXIMUM DE SIMILITUDE
*       AUX LATITUDES MOYENNES. LES VALEURS TROPICALES SONT LE RESULTAT
*       D'UNE INTEGRATION PARTANT DES SOUS-TROPIQUES VERS L'EQUATEUR
*       DE TELLE SORTE QUE LES DERIVEE LATITUDINALES Y SOIENT NULLES.
*
*
*     FICHIER I/O...
*      IN  = GRILLES HEMISPHERIQUES TYPIQUES
*      OUT = GRILLES SYMMETRIQUES CORRESPONDANTES
*
*
*     EXIT CONDITIONS...
*       0 NO PROBLEMS
*       1 DATA TYPE IS NOT GRID OR DATA IS ALREADY GLOBAL
*       2 PREMATURE EOF ON FILE IN
*       3 MORE THAN ONE SIZE FOR ARRAYS IN FILE IN
#     endif
#     if defined (RDIAG_LICENCE)
!---------------------------------- LICENCE BEGIN -------------------------------
! R.DIAG - Diagnostic tool kit for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This code is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------
#     endif
#     if !defined (NO_SOURCE)
      PROGRAM mksymm
*    1               (      IN,       OUT,       OUTPUT,
*    2                TAPE1=IN, TAPE2=OU1, TAPE6=OUTPUT)
  
*     $Log: mksymm.ptn,v $
*     Revision 4.5  2014/09/25 20:20:51  dugas
*     Inserer le texte de la licence LPGPL 2.1 pour R.DIAG.
*
*     Revision 4.4  2008/05/03 21:06:30  dugas
*     Modification generale pour r.gppf.
*
*     Revision 4.3  2007/12/20 21:19:13  dugas
*     - Utiliser ALLOCATE/DEALLOCATE plutot que HPALLOC/HPDEALLC.
*     - Faire appel aux routines PRTLAB/PRTLAB2.
*
*     Revision 4.2  2003/09/15 15:49:33  dugas
*     Enlever toutes references a MEMOIR.
*
*     Revision 4.1  1997/09/26 17:39:09  armnrbd
*     Remplacer buf(16) par buf(taille_entete).
*
*     Revision 4.0  1994/11/17  14:21:09  armnrbd
*     Messages informatifs quand au passage de la version 3.x a 4.0...
*     1) Les espaces en debut des noms de variables de sont plus pertinents.
*     2) Les grilles complexes de type CMPL sont maintenant supportees.
*     3) Les fichiers SQI sont reconnus, lus et ecrit directements.
*     4) Plusieurs nouvelles cles sont disponibles au demarrage.
*
*     Revision 3.0  93/10/13  13:56:33  armnrbd
*     Premiere version compatible HP-UX.
*     
*     Revision 1.4  92/11/10  15:41:09  15:41:09  armnrbd (Bernard Dugas)
*     Implantation de GETSIZ/GETSIZ2.
*     
*     Revision 1.3  92/05/01  16:15:32  armnrbd
*     Corriger l'appel a JCLPNT.
*     
*     Revision 1.2  92/05/01  15:54:34  armnrbd
*     Ajouter la documentation automatique.
*     
*     Revision 1.1  92/04/24  15:48:15  armnrbd
*     Version "Call Memoir".
*     
*     Revision 1.0  92/02/24  14:41:51  armnrbd
*     Initial revision
*     
***    AUTEUR: B. DUGAS, NOV 30/88.
  
***    MODIFIEE LE 20 DECEMBRE 1988 PAR B.DUGAS ...
***     ...  LE RESULTAT EST TOUJOURS GLOBAL ET LA GRILLE INITIALE
***          PEUT PROVENIR DE L'HEMISPHERE NORD OU SUD.
***    MODIFIEE LE 07 MAI 1989 PAR B.DUGAS ...
***     ...  CORRECTION D'UN BOGUE MAJEUR DANS LA BLOUCLE 200.
  
*--------------------------------------------------------------
      IMPLICIT    none
  
      LOGICAL     OK
      CHARACTER   KIND*4
      REAL        DEMI,XN,OJ1,ZERO
      INTEGER     JBUF(taille_entete),HEAD,TYPE,WHERE,HEM,
     +            NF,ILG,ILAT,ILATH,LEN,NWDS,NPK,MAXW,NS,MEM1,
     +            MEM2,MEM3,I,J,IJ,IJ1,IJ2,NB,IER

      INTEGER,    DIMENSION(:), ALLOCATABLE :: IBUF
      REAL,       DIMENSION(:), ALLOCATABLE :: F,ACC

      CHARACTER   NOMPRG*256
      COMMON     /PROGNAM/ NOMPRG

      INTEGER     GETSIZ
      EXTERNAL    MACSPE,JCLPNT,GETSIZ,XIT,
     +            LBLCHK,GETFLD2,PUTFLD2

      DATA        DEMI / 0.5 /
*===================================================================

      NOMPRG =
     +'$Source: /home/cvs/RMNLIB/utils/rdiag/lspgm/mksymm.ptn,v $'

*--------------------------------------------------------------
***    ALLER CHERCHER QUELQUES CONSTANTES SPECIFIQUES.

      CALL MACSPE( HEAD,ZERO )

***    ACCEDER LES FICHIERS D'E/S.

      NF = 3
      CALL JCLPNT( NF, 1,2, 6 )
  
***    QUELLE SORTE DE DONNEES DANS IN ?
  
      MAXW = GETSIZ( 1, JBUF,LEN,NWDS,NPK )
      IF (MAXW.EQ.0) GOTO 999
  
      TYPE  = JBUF(1)
      ILG   = JBUF(5)
      ILATH = JBUF(6)
      ILAT  = ILATH*2
      HEM   = JBUF(7)

      WRITE(KIND,4) TYPE
      IF (KIND.NE.'GRID' .OR. HEM.EQ.0) CALL       XIT (' Mkunsy ',-1 )
  
***    DETERMINER LA QUANTITE DE MEMOIRE REQUISE ET ...
  
      JBUF(6) = ILAT
      CALL LBLCHK( LEN, NWDS,NPK,JBUF )

      MAXW = LEN-HEAD

      MEM1 = LEN
      MEM2 = ILG
      MEM3 = NWDS
  
***    ... EN FAIRE LA DEMANDE.
  
      ALLOCATE( IBUF(MEM1),ACC(MEM2),F(MEM3) )
 
      NB   = ILATH/5+1
      XN   = MAX(1.0, 50.0/FLOAT(ILATH))
  
      IF (HEM.EQ.2)                                            THEN
          WHERE = 1 
      ELSE
          WHERE = ILATH*ILG+1
      END IF
  
***    METTRE LES DERIVEES LATITUDINALES A ZERO A L'EQUATEUR.
  
      NS = 0
  
  100 CALL GETFLD2( 1, F(WHERE),'GRID',-1,' ',-1, IBUF,MAXW,OK )
  
          IF (.NOT.OK)                                         THEN
                             WRITE(6,6020)  NS
                             CALL PRTLAB2(' Last record out : ',IBUF )
              IF (NS.EQ.0)   CALL           XIT(' Mksymm ',-2 )
                             CALL           XIT(' Mksymm ', 0 ) 
          END IF
  
          IF (IBUF(5).NE.ILG  .OR.
     1        IBUF(6).NE.ILATH .OR.
     2        IBUF(7).NE.HEM      )                         THEN
                             CALL PRTLAB2(' Last record out : ',IBUF )
                             CALL           XIT(' Mksymm ',-3 )
          END IF
  
***        GLOBALISER LA GRILLE IMMEDIATEMENT.
  
          DO 200 J=1,ILATH
              IF (HEM.EQ.2)                                    THEN
                  IJ1 = ( J  -1)*ILG
                  IJ2 = (ILAT-J)*ILG
              ELSE
                  IJ1 = (ILAT-J)*ILG
                  IJ2 = ( J  -1)*ILG
              END IF
              DO 200 I=1,ILG
                  F(IJ2+I) = F(IJ1+I)
  200     CONTINUE
  
***        ON INITIALISE L'ACCUMULATEUR AVEC LA VALEUR DE
***        LA NB-IEME LATITUDE DE L'EQUATEUR.
  
          IJ = (ILATH+NB-1)*ILG
          DO 300 I=1,ILG
              ACC(I) = F(IJ+I)
  300     CONTINUE
  
***        ON INTEGRE DE CETTE LATITUDE VERS L'EQUATEUR.
  
          DO 500 J=1,NB-1
              IJ1 = (ILATH+NB-1-J)*ILG 
              IJ2 = (ILATH-NB  +J)*ILG
              OJ1 = 1.0/FLOAT(J+1)
              DO 400 I=1,ILG
                  ACC(I)   = ACC(I)+F(IJ1+I)
                  F(IJ1+I) = ACC(I)*OJ1 
                  F(IJ2+I) = ACC(I)*OJ1 
  400         CONTINUE
              F(IJ1+I) = F(IJ1+1)
              F(IJ2+I) = F(IJ2+1)
  500     CONTINUE
  
***        SAUVER LE GRILLE GLOBALE.
  
          IBUF(6) = ILAT
          IBUF(7) = 0
          CALL PUTFLD2( 2, F, IBUF,MAXW )
  
          NS = NS+1 
  
      GOTO 100
  
***    FIN PREMATUREE DES DONNEES SUR L'UNITE 1. 
  
  999 CALL                                         XIT(' Mksymm ',-2 )
*-----------------------------------------------------------------
  
    4 FORMAT(A4)

 6020 FORMAT('0Mksymm produced ',I5,' global record(s).')

      END 
#     endif
